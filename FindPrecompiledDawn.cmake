# - Try to find precompiled libwebgpu_dawn
# Once done this will define
#  PRECOMPILED_DAWN_FOUND - System has precompiled libwebgpu_dawn
#  PRECOMPILED_DAWN_INCLUDE_DIRS - The precompiled libwebgpu_dawn include directories
#  PRECOMPILED_DAWN_LIBRARY - The library to link to
#  PRECOMPILED_DAWN_BINARY - The DLL or shared object file

#
# Figure out the default folder to search for libraries in
#
string(TOLOWER "${CMAKE_SYSTEM_NAME}" _PRECOMPILED_DAWN_SYSTEM_NAME)
string(TOLOWER "${CMAKE_SYSTEM_PROCESSOR}" _PRECOMPILED_DAWN_SYSTEM_PROCESSOR)
string(TOLOWER "${CMAKE_LIBRARY_ARCHITECTURE}" _PRECOMPILED_DAWN_TARGET_TRIPLE)
string(TOLOWER "${CMAKE_MSVC_RUNTIME_LIBRARY}" _PRECOMPILED_DAWN_RUNTIME_LIBRARY)

if (WIN32)
    set(PRECOMPILED_DAWN_LIBRARY_NAME "webgpu_dawn_dll" CACHE STRING
        "The webgpu_dawn library name")
else()
    set(PRECOMPILED_DAWN_LIBRARY_NAME "webgpu_dawn" CACHE STRING
        "The webgpu_dawn library name")
endif()
set(PRECOMPILED_DAWN_SYSTEM_NAME "${_PRECOMPILED_DAWN_SYSTEM_NAME}" CACHE STRING
    "The target system name")
set(PRECOMPILED_DAWN_SYSTEM_PROCESSOR "${_PRECOMPILED_DAWN_SYSTEM_PROCESSOR}" CACHE STRING
    "The target system processor")
set(PRECOMPILED_DAWN_TARGET_TRIPLE "${_PRECOMPILED_DAWN_TARGET_TRIPLE}" CACHE STRING
    "The target system processor")
set(PRECOMPILED_DAWN_RUNTIME_LIBRARY "${_PRECOMPILED_DAWN_RUNTIME_LIBRARY}" CACHE STRING
    "The target MSVC runtime library (Windows only)")

if ("${PRECOMPILED_DAWN_SYSTEM_NAME}" STREQUAL "")
    message(FATAL_ERROR "PRECOMPILED_DAWN_SYSTEM_NAME is null")
endif()

# On platforms that provide it, e.g. Linux, prefer the target triple, if it is available
if (PRECOMPILED_DAWN_TARGET_TRIPLE STREQUAL "")
    if ("${PRECOMPILED_DAWN_SYSTEM_PROCESSOR}" STREQUAL "")
        message(FATAL_ERROR "PRECOMPILED_DAWN_SYSTEM_PROCESSOR is null")
    endif()
    set(PRECOMPILED_DAWN_LIBDIR_BASE "${PRECOMPILED_DAWN_SYSTEM_NAME}/lib/${PRECOMPILED_DAWN_SYSTEM_PROCESSOR}")
    set(PRECOMPILED_DAWN_BINDIR_BASE "${PRECOMPILED_DAWN_SYSTEM_NAME}/bin/${PRECOMPILED_DAWN_SYSTEM_PROCESSOR}")
else()
    set(PRECOMPILED_DAWN_LIBDIR_BASE "${PRECOMPILED_DAWN_SYSTEM_NAME}/lib/${PRECOMPILED_DAWN_TARGET_TRIPLE}")
    set(PRECOMPILED_DAWN_BINDIR_BASE "${PRECOMPILED_DAWN_SYSTEM_NAME}/bin/${PRECOMPILED_DAWN_TARGET_TRIPLE}")
endif()
set(PRECOMPILED_DAWN_INCLUDEDIR_BASE "${PRECOMPILED_DAWN_SYSTEM_NAME}/include")

if (WIN32)
    if ("${PRECOMPILED_DAWN_RUNTIME_LIBRARY}" STREQUAL "")
        message(FATAL_ERROR "PRECOMPILED_DAWN_RUNTIME_LIBRARY is null")
    endif()
    set(PRECOMPILED_DAWN_LIBDIR_BASE "${PRECOMPILED_DAWN_LIBDIR_BASE}/${PRECOMPILED_DAWN_RUNTIME_LIBRARY}")
    set(PRECOMPILED_DAWN_BINDIR_BASE "${PRECOMPILED_DAWN_BINDIR_BASE}/${PRECOMPILED_DAWN_RUNTIME_LIBRARY}")
endif()

#
# Set up find_package
#
set(PRECOMPILED_DAWN_ROOT "${CMAKE_CURRENT_LIST_DIR}" CACHE STRING
    "The path to the precompiled Dawn installation")

find_library(
    PRECOMPILED_DAWN_LIBRARY
    NAMES
        ${PRECOMPILED_DAWN_LIBRARY_NAME}
    HINTS
        ${PRECOMPILED_DAWN_ROOT}/${PRECOMPILED_DAWN_LIBDIR_BASE}
)

find_library(
    PRECOMPILED_DAWN_BINARY
    NAMES
        ${PRECOMPILED_DAWN_LIBRARY_NAME}
    HINTS
        ${PRECOMPILED_DAWN_ROOT}/${PRECOMPILED_DAWN_BINDIR_BASE}
        ${PRECOMPILED_DAWN_ROOT}/${PRECOMPILED_DAWN_LIBDIR_BASE}
)

find_path(
    PRECOMPILED_DAWN_INCLUDEDIR webgpu/webgpu.h
    HINTS
        ${PRECOMPILED_DAWN_ROOT}/${PRECOMPILED_DAWN_INCLUDEDIR_BASE}
)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
    PrecompiledDawn
    DEFAULT_MSG
    PRECOMPILED_DAWN_LIBRARY
    PRECOMPILED_DAWN_BINARY
    PRECOMPILED_DAWN_INCLUDEDIR
)

mark_as_advanced(
    PRECOMPILED_DAWN_INCLUDEDIR
    PRECOMPILED_DAWN_LIBRARY
    PRECOMPILED_DAWN_BINARY
)

# Pluralize (to be conformant with expectations)
set(PRECOMPILED_DAWN_LIBRARIES ${PRECOMPILED_DAWN_LIBRARY} )
set(PRECOMPILED_DAWN_INCLUDE_DIRS ${PRECOMPILED_DAWN_INCLUDEDIR})

# Create a target to link with
add_library(PrecompiledDawn INTERFACE)
target_include_directories(PrecompiledDawn INTERFACE
    ${PRECOMPILED_DAWN_INCLUDE_DIRS})
target_link_libraries(PrecompiledDawn INTERFACE
    ${PRECOMPILED_DAWN_LIBRARIES})
